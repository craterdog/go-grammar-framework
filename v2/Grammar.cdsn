!>
................................................................................
.    Copyright (c) 2009-2024 Crater Dog Technologies.  All Rights Reserved.    .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See http://opensource.org/licenses/MIT)                        .
................................................................................
<!

!>
CRATER DOG SYNTAX NOTATION
This document contains a formal definition of the Crater Dog Syntax Notationâ„¢
(CDSN) using CDSN itself in homage to Douglas Hofstadter.  A language grammar
consists of a set of token definitions and rule definitions.

The following intrinsic character types are context specific:
 * ANY - Any language specific character.
 * LOWER - Any language specific lower case character.
 * UPPER - Any language specific upper case character.
 * DIGIT - Any language specific digit.
 * ESCAPE - Any environment specific escape sequence.
 * CONTROL - Any environment specific (non-printable) control character.
 * EOL - The environment specific end-of-line character.
 * EOF - The environment specific end-of-file marker (pseudo character).

A predicate may be constrained by any of the following cardinalities:
 * predicate{M} - Exactly M instances of the specified predicate.
 * predicate{M..} - M or more instances of the specified predicate.
 * predicate{M..N} - M to N instances of the specified predicate.
 * predicate? - Zero or one instances of the specified predicate.
 * predicate* - Zero or more instances of the specified predicate.
 * predicate+ - One or more instances of the specified predicate.

An inversion "~" within a definition may only be applied to an intrinsic
character type or a glyph range.
<!

!>
TOKEN DEFINITIONS
The following token definitions are used by the scanner to generate the
stream of tokens that are processed by the parser.  Each token name begins
with an upper case letter.  Unlike with rule definitions, a token definition
cannot specify the name of a rule within its definition but it can specify
the name of other tokens.  Token definitions cannot be recursive and the
scanning of tokens is NOT greedy.  And any spaces within a token definition
are NOT ignored.
<!
Character: "'" ~CONTROL "'"  ! Any printable character.

Comment: "!>" EOL ANY* EOL "<!" EOL  ! Chooses the shortest possible match.

Delimiter: "~" | "?" | "*" | "+" | ":" | "|" | "(" | ")" | "{" | "}" | ".."

Intrinsic: "ANY" | "LOWER" | "UPPER" | "DIGIT" | "ESCAPE" | "CONTROL" | "EOL" | "EOF"

Letter: LOWER | UPPER

Literal: '"' (ESCAPE | ~CONTROL)+ '"'

Name: Letter (Letter | DIGIT)*

Note: "! " (~CONTROL)*

Number: DIGIT+

!>
RULE DEFINITIONS
The following rules are used by the parser when parsing the stream of tokens
generated by the scanner.  Each rule name begins with a lower case letter.
The rule definitions may specify the names of tokens or other rules and are
matched by the parser in the order listed.  A rule definition may also be
directly or indirectly recursive.  The sequence of factors within in a rule
definition may be separated by spaces which are ignored by the parser.
<!
source: grammar EOF  ! Terminated with an end-of-file marker.

grammar: header+ definition+

header: Comment EOL+

definition: Comment? Name ":" expression EOL+

expression: inline | multiline

inline: alternative ("|" alternative)* Note?

multiline: line+

line: EOL alternative Note?

alternative: factor+

factor: predicate cardinality?  ! The default cardinality is one.

predicate: element | inversion | precedence

element: Literal | Name

inversion: "~"? filter

filter: Intrinsic | glyph

glyph: Character (".." Character)?  ! The range of characters is inclusive.

precedence: "(" expression EOL? ")"

cardinality:
    "?"  ! Zero or one instance of a predicate.
    "*"  ! Zero or more instances of a predicate.
    "+"  ! One or more instances of a predicate.
    "{" constraint "}"  ! Constrains the number of instances of a predicate.

constraint: Number (".." Number?)?  ! The range of numbers is inclusive.

