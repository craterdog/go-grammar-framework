!>
 *******************************************************************************
 *   Copyright (c) 2009-2024 Crater Dog Technologies™.  All Rights Reserved.   *
 *******************************************************************************
 * DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               *
 *                                                                             *
 * This code is free software; you can redistribute it and/or modify it under  *
 * the terms of The MIT License (MIT), as published by the Open Source         *
 * Initiative. (See http://opensource.org/licenses/MIT)                        *
 *******************************************************************************
<!

!>
    CRATER DOG SYNTAX NOTATION
    This document contains a formal definition of the Crater Dog Syntax Notation™
    (CDSN) using CDSN itself in homage to Douglas Hofstadter.  A language grammar
    consists of a set of token and rule definitions.

    TOKEN DEFINITIONS
    The following token definitions are used by the scanner to generate the
    stream of tokens that are processed by the parser.  Each token name begins
    with an upper case letter.  Unlike with rule definitions, a token definition
    cannot specify the name of a rule within its definition but it can specify
    the name of other tokens.

    The following intrinsic token types are environment or language specific:
     * ANY - Any language specific character.
     * LOWER - Any language specific lower case character.
     * UPPER - Any language specific upper case character.
     * DIGIT - Any language specific digit.
     * ESCAPE - Any environment specific escape sequence.
     * CONTROL - Any environment specific (non-printable) control character.
     * EOL - The environment specific end-of-line character.
     * EOF - The environment specific end-of-file marker.

    Token definitions cannot be recursive and the scanning of tokens is not
    greedy.  Any spaces within a token definition are NOT ignored.
<!

$CHARACTER: "'" ~CONTROL "'"  ! Any printable character.

$COMMENT: "!>" EOL ANY* EOL "<!" EOL+  ! Chooses the shortest possible match.

$DELIMITER: "~" | "?" | "*" | "+" | ":" | "|" | "(" | ")" | "{" | "}" | ".."

$INTRINSIC: "ANY" | "LOWER" | "UPPER" | "DIGIT" | "ESCAPE" | "CONTROL" | "EOL" | "EOF"

$LETTER: LOWER | UPPER

$LITERAL: '"' (ESCAPE | ~('"' | CONTROL))+ '"'

$NAME: LETTER ('_'? (LETTER | DIGIT))*

$NOTE: "! " (~CONTROL)*

$NUMBER: DIGIT+

$SYMBOL: '$' NAME

!>
    RULE DEFINITIONS
    The following rules are used by the parser when parsing the stream of tokens
    generated by the scanner.  Each rule name begins with a lower case letter.
    The rule definitions may specify the names of tokens or other rules and are
    matched by the parser in the order listed.  A rule definition may also be
    directly or indirectly recursive.  The sequence of factors within in a rule
    definition may be separated by spaces which are ignored by the parser.

    A predicate within a factor may also be constrained by any of the following
    cardinalities:
     * predicate{M} - Exactly M instances of the specified predicate.
     * predicate{M..} - M or more instances of the specified predicate.
     * predicate{M..N} - M to N instances of the specified predicate.
     * predicate? - Zero or one instances of the specified predicate.
     * predicate* - Zero or more instances of the specified predicate.
     * predicate+ - One or more instances of the specified predicate.

    Inversion within a rule definition may only be applied to an assertion
    resulting in a single character or glyph.
<!

$source: grammar EOF  ! Terminated with an end-of-file marker.

$grammar: COMMENT statement+

$statement: COMMENT? definition EOL+

$definition: SYMBOL ":" expression  ! This works for tokens and rules.

$expression:
    alternative ("|" alternative)*
    (EOL alternative)+


$alternative: factor+ NOTE?

$factor: predicate cardinality?  ! The default cardinality is one.

$predicate: "~"? assertion

$assertion: element | glyph | precedence

$element: INTRINSIC | LITERAL | NAME

$glyph: CHARACTER (".." CHARACTER)?  ! The range of characters is inclusive.

$precedence: "(" expression ")"

$cardinality:
    "?"  ! Zero or one instance of a predicate.
    "*"  ! Zero or more instances of a predicate.
    "+"  ! One or more instances of a predicate.
    "{" constraint "}"  ! Constrains the number of instances of a predicate.


$constraint: NUMBER (".." NUMBER?)?  ! The range of numbers is inclusive.

